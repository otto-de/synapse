plugins {
    id "java-library"
}

configurations.configureEach {
    exclude group: 'javax.annotation'
}

dependencies {
    // This dependency is exported to consumers, that is to say found on their compile classpath.
    api 'com.google.guava:guava:33.3.1-jre'
    api "org.springframework.boot:spring-boot"
    api "org.springframework.boot:spring-boot-autoconfigure"

    implementation(platform("net.openhft:chronicle-bom:2.27ea45"))
    implementation "jakarta.annotation:jakarta.annotation-api:3.0.0"

    // This dependency is used internally, and not exposed to consumers on their own compile classpath.
    implementation "org.springframework:spring-context"

    implementation "org.springframework:spring-aop"
    implementation "org.aspectj:aspectjweaver"

    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jsr310"
    implementation "com.fasterxml.jackson.datatype:jackson-datatype-jdk8"

    implementation("net.openhft:chronicle-map") {
        exclude group: "com.intellij", module: "annotations"
    }

    implementation "com.googlecode.concurrentlinkedhashmap:concurrentlinkedhashmap-lru:1.4.2"

    implementation("org.dizitart:nitrite:3.4.4") {
        exclude group: "javax.validation", module: "validation-api"
    }

    testImplementation project(':synapse-testsupport:')

    testImplementation 'junit:junit'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "com.jayway.jsonpath:json-path-assert"
    testImplementation 'com.tngtech.java:junit-dataprovider:1.13.1'
    testImplementation "org.awaitility:awaitility"
    testImplementation "org.mockito:mockito-core"
    testImplementation "ch.qos.logback:logback-core"
    testImplementation "ch.qos.logback:logback-classic"
}

apply plugin: 'maven-publish'
apply plugin: 'signing'


jar {
    manifest.attributes provider: 'gradle'
}

// these jvm args are required so that chronicle map works with java 17 (see https://chronicle.software/chronicle-support-java-17/)
tasks.named('test', Test) {
    jvmArgs("--add-exports=java.base/jdk.internal.ref=ALL-UNNAMED",
            "--add-exports=java.base/sun.nio.ch=ALL-UNNAMED",
            "--add-exports=jdk.unsupported/sun.misc=ALL-UNNAMED",
            "--add-exports=jdk.compiler/com.sun.tools.javac.file=ALL-UNNAMED",
            "--add-opens=jdk.compiler/com.sun.tools.javac=ALL-UNNAMED",
            "--add-opens=java.base/java.lang=ALL-UNNAMED",
            "--add-opens=java.base/java.lang.reflect=ALL-UNNAMED",
            "--add-opens=java.base/java.io=ALL-UNNAMED",
            "--add-opens=java.base/java.util=ALL-UNNAMED")
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'synapse-core'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'synapse-core'
                packaging 'jar'
                description = 'A library used at otto.de to implement Spring Boot based event-sourcing microservices.'
                url = 'http://github.com/otto-de/synapse'

                scm {
                    url = 'scm:git@github.com:otto-de/synapse.git'
                    connection = 'scm:git@github.com:otto-de/synapse.git'
                    developerConnection = 'scm:git@github.com:otto-de/synapse.git'
                }

                licenses {
                    license {
                        name = "The Apache Software License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'gsteinacker'
                        name = 'Guido Steinacker'
                    }
                    developer {
                        id = 'mbargmann'
                        name = 'Matthias Bargmann'
                    }
                    developer {
                        id = 'fbregulla'
                        name = 'Frank Bregulla'
                    }
                    developer {
                        id = 'ftorkler'
                        name = 'Florian Torkler'
                    }
                    developer {
                        id = 'miregel'
                        name = 'Micha Regel'
                    }
                    developer {
                        id = 'hstruebe'
                        name = 'Henning Strueber'
                    }
                }
            }
        }
    }

}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

