buildscript {
    repositories {
        mavenCentral()
        //Needed only for SNAPSHOT versions
        //maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.11.0"
    }
}

ext {
    awsSdkVersion = "2.16.20"
    springVersion = "5.3.5"
    springSecurityVersion = "5.4.1"
    springBootVersion = "2.4.0"
    edisonVersion = "2.4.0"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'io.codearte.nexus-staging'
apply from: "${rootDir}/jacoco.gradle"

task startLocalStack(type:Exec) {
    commandLine "docker", "run", "--name", "synapse_localstack", "-d", "-p", "4566-4583:4566-4583", "localstack/localstack:0.11.2"
}
task stopLocalStack(type: Exec) {
    commandLine 'docker', 'rm', '-f', 'synapse_localstack'
}

nexusStaging {
    username=sonatypeUsername
    password=sonatypePassword
    packageGroup='de.otto'
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply from: "${rootDir}/jacoco.gradle"

    sourceCompatibility = '1.8'
    targetCompatibility = '1.8'

    dependencies {
        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:${springBootVersion}"
    }

    task allDeps(type: DependencyReportTask) {}

    compileJava.dependsOn(processResources)

    version = '0.23.2'
    group = 'de.otto.synapse'

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/releases/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }

}
