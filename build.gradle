buildscript {
    repositories {
        mavenCentral()
        maven {
            url "https://plugins.gradle.org/m2/"
        }
        //Needed only for SNAPSHOT versions
        //maven { url "http://oss.sonatype.org/content/repositories/snapshots/" }
    }
    dependencies {
        classpath "io.codearte.gradle.nexus:gradle-nexus-staging-plugin:0.30.0"
    }
}


ext {
    awsSdkVersion = "2.20.12"
    springBootVersion = "3.0.6"
    edisonVersion = "3.0.0-M2"
    jacksonVersion = "2.14.2"
    logbackVersion = "1.2.11"
}

apply plugin: 'java'
apply plugin: 'idea'
apply plugin: 'eclipse'
apply plugin: 'io.codearte.nexus-staging'
apply from: "${rootDir}/jacoco.gradle"

task startLocalStack(type: Exec) {
    commandLine "docker", "run", "--name", "synapse_localstack", "-d", "-p", "4566:4566", "localstack/localstack:1.4.0"
}
task stopLocalStack(type: Exec) {
    commandLine 'docker', 'rm', '-f', 'synapse_localstack'
}

nexusStaging {
    username = sonatypeUsername
    password = sonatypePassword
    packageGroup = 'de.otto'
}

subprojects {

    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply from: "${rootDir}/jacoco.gradle"

    sourceCompatibility = '17'
    targetCompatibility = '17'

    dependencies {
        annotationProcessor "org.springframework.boot:spring-boot-configuration-processor:${springBootVersion}"

        implementation(platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"))

        testImplementation(platform("org.springframework.boot:spring-boot-dependencies:${springBootVersion}"))

        implementation(platform("software.amazon.awssdk:bom:${awsSdkVersion}"))

    }

    task allDeps(type: DependencyReportTask) {}

    compileJava.dependsOn(processResources)

    version = '0.25.3-SNAPSHOT'
    group = 'de.otto.synapse'

    repositories {
        mavenLocal()
        mavenCentral()
        maven { url 'https://oss.sonatype.org/content/repositories/releases/' }
        maven { url 'https://oss.sonatype.org/content/repositories/snapshots/' }
    }

}
