apply plugin: 'java-library'

dependencies {
    api project(':synapse-core:')
    api project(':synapse-aws-auth:')

    implementation "com.google.guava:guava:31.1-jre"
    implementation "software.amazon.awssdk:sqs"

    // Use JUnit test framework
    testImplementation 'junit:junit:4.13.2'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation "org.springframework.boot:spring-boot-starter-test"
    testImplementation "org.springframework.boot:spring-boot-starter-web"
    testImplementation "com.jayway.jsonpath:json-path-assert:2.7.0"
    testImplementation 'com.tngtech.java:junit-dataprovider:1.13.1'
    testImplementation "org.awaitility:awaitility:4.2.0"
    testImplementation "org.mockito:mockito-core:5.1.1"
    testImplementation "ch.qos.logback:logback-core:${logbackVersion}"
    testImplementation "ch.qos.logback:logback-classic:${logbackVersion}"

}

apply plugin: 'maven-publish'
apply plugin: 'signing'

jar {
    manifest.attributes provider: 'gradle'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}


publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'synapse-aws-sqs'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'synapse-aws-sqs'
                packaging 'jar'
                description = 'SQS implementations for message queues in Synapse services.'
                url = 'http://github.com/otto-de/synapse'

                scm {
                    url = 'scm:git@github.com:otto-de/synapse.git'
                    connection = 'scm:git@github.com:otto-de/synapse.git'
                    developerConnection = 'scm:git@github.com:otto-de/synapse.git'
                }

                licenses {
                    license {
                        name = "The Apache Software License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'gsteinacker'
                        name = 'Guido Steinacker'
                    }
                    developer {
                        id = 'mbargmann'
                        name = 'Matthias Bargmann'
                    }
                    developer {
                        id = 'fbregulla'
                        name = 'Frank Bregulla'
                    }
                    developer {
                        id = 'ftorkler'
                        name = 'Florian Torkler'
                    }
                    developer {
                        id = 'miregel'
                        name = 'Micha Regel'
                    }
                    developer {
                        id = 'hstruebe'
                        name = 'Henning Strueber'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "Sonatype"
            url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = findProperty("sonatypeUsername")
                password = findProperty("sonatypePassword")
            }
        }
        maven {
            name = "Sonatype_Snapshots"
            url = uri("https://oss.sonatype.org/content/repositories/snapshots/")
            credentials {
                username = findProperty("sonatypeUsername")
                password = findProperty("sonatypePassword")
            }
        }
    }
}


signing {
    sign publishing.publications.mavenJava
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}

