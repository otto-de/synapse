apply plugin: 'java-library'

dependencies {
    api project(':synapse-core:')
    implementation "de.otto.edison:edison-core:${edisonVersion}"
    implementation "org.springframework.boot:spring-boot-actuator"
    implementation "org.springframework:spring-context"
    implementation "software.amazon.awssdk:s3"
    implementation "software.amazon.awssdk:kinesis"
    implementation "de.otto.edison:edison-hal:2.1.1"
    implementation "io.micrometer:micrometer-core"

    testImplementation project(':synapse-testsupport:')
    testImplementation 'junit:junit'
    testImplementation 'org.hamcrest:hamcrest-all:1.3'
    testImplementation "de.otto.edison:edison-testsupport:${edisonVersion}"
    testImplementation "org.mockito:mockito-core"
    testImplementation "ch.qos.logback:logback-core"
    testImplementation "ch.qos.logback:logback-classic"
}

apply plugin: 'maven-publish'
apply plugin: 'signing'

jar {
    manifest.attributes provider: 'gradle'
}

task javadocJar(type: Jar, dependsOn: javadoc) {
    archiveClassifier = 'javadoc'
    from 'build/docs/javadoc'
}

task sourcesJar(type: Jar) {
    from sourceSets.main.allSource
    archiveClassifier = 'sources'
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'synapse-edison'
            from components.java
            versionMapping {
                usage('java-api') {
                    fromResolutionOf('runtimeClasspath')
                }
                usage('java-runtime') {
                    fromResolutionResult()
                }
            }
            pom {
                name = 'synapse-edison'
                packaging 'jar'
                description = 'A library used at otto.de to integrate synapse-core with edison-microservice.'
                url = 'http://github.com/otto-de/synapse'

                scm {
                    url = 'scm:git@github.com:otto-de/synapse.git'
                    connection = 'scm:git@github.com:otto-de/synapse.git'
                    developerConnection = 'scm:git@github.com:otto-de/synapse.git'
                }

                licenses {
                    license {
                        name = "The Apache Software License, Version 2.0"
                        url = "http://www.apache.org/licenses/LICENSE-2.0.txt"
                        distribution = 'repo'
                    }
                }
                developers {
                    developer {
                        id = 'gsteinacker'
                        name = 'Guido Steinacker'
                    }
                    developer {
                        id = 'mbargmann'
                        name = 'Matthias Bargmann'
                    }
                    developer {
                        id = 'fbregulla'
                        name = 'Frank Bregulla'
                    }
                    developer {
                        id = 'ftorkler'
                        name = 'Florian Torkler'
                    }
                    developer {
                        id = 'miregel'
                        name = 'Micha Regel'
                    }
                    developer {
                        id = 'hstruebe'
                        name = 'Henning Strueber'
                    }
                }
            }
        }
    }

    repositories {
        maven {
            name = "Sonatype"
            url = uri("https://oss.sonatype.org/service/local/staging/deploy/maven2/")
            credentials {
                username = findProperty("sonatypeUsername")
                password = findProperty("sonatypePassword")
            }
        }
        maven {
            name = "Sonatype_Snapshots"
            url = uri("https://oss.sonatype.org/content/repositories/snapshots/")
            credentials {
                username = findProperty("sonatypeUsername")
                password = findProperty("sonatypePassword")
            }
        }
    }
}

signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}

artifacts {
    archives jar
    archives javadocJar
    archives sourcesJar
}


